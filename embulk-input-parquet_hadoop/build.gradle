import com.github.jrubygradle.JRubyExec

dependencies {
    compile  "org.embulk:embulk-core:0.8.16"
    provided "org.embulk:embulk-core:0.8.16"

    compile project(':parquet-msgpack')
    // for hadoop
    compile 'org.slf4j:log4j-over-slf4j:1.7.24'
    // for parquet
    compile 'org.slf4j:jul-to-slf4j:1.7.24'

    testCompile "junit:junit:4.+"
    testCompile 'org.embulk:embulk-standards:0.8.16'
    testCompile "org.embulk:embulk-test:0.8.16"
    testCompile 'org.assertj:assertj-core:2.6.+'
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
    scriptArgs "${project.projectDir.absolutePath}/build/gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "${project.projectDir}/pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "push"
    scriptArgs "pkg/${project.name}-${project.version}.gem"
}

task gemspec {
    doLast {
        file('build').mkdirs()
        file('build/gemspec').write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Koji AGAWA"]
  spec.summary       = %[Parquet input plugin for Embulk]
  spec.description   = %[Loads records from Parquet files via Hadoop FileSystem.]
  spec.email         = ["agawa_koji@cyberagent.co.jp"]
  spec.licenses      = ["Apache 2.0"]
  spec.homepage      = "https://github.com/CyberAgent/embulk-input-parquet_hadoop"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['>= 10.0']
end
/$)
    }
}
