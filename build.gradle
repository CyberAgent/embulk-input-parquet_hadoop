plugins {
    id "com.jfrog.bintray" version "1.6"
    id "com.github.jruby-gradle.base" version "1.2.1"
    id "java"
    id "checkstyle"
}
import com.github.jrubygradle.JRubyExec

allprojects {
    group = 'jp.co.cyberagent.embulk'
    version = '0.1.1'
}

subprojects {
    apply plugin: 'com.github.jruby-gradle.base'
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        provided
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" //<< "-Xlint:deprecation"
        }
    }

    task classpath(type: Copy, dependsOn: ["jar"]) {
        doFirst { file("classpath").deleteDir() }
        from (configurations.runtime - configurations.provided + files(jar.archivePath))
        into "classpath"
    }
    clean {
        delete "classpath"
        delete "${project.name}.gemspec"
    }

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '6.14.1'
    }
    checkstyleMain {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    checkstyleTest {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    task checkstyle(type: Checkstyle) {
        classpath = sourceSets.main.output + sourceSets.test.output
        source = sourceSets.main.allJava + sourceSets.test.allJava
    }

    task "package"(dependsOn: ["gemspec", "classpath"]) {
        doLast {
            println "> Build succeeded."
            println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
        }
    }
}

task gemPush() {
    dependsOn ":embulk-input-parquet_hadoop:gemPush"
}
